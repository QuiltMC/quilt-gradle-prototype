plugins {
	id 'org.quiltmc.gradle.licenser' version '1.1.+' apply false
	id 'net.kyori.blossom' version '1.3.+' apply false
}

subprojects {
	apply plugin: 'java-gradle-plugin'
	apply plugin: 'maven-publish'
	apply plugin: 'org.quiltmc.gradle.licenser'

	version '0.1.' + System.getenv().getOrDefault('BUILD_NUMBER', 'local')

	repositories {
		mavenCentral()

		maven {
			name 'Quilt'
			url 'https://maven.quiltmc.org/repository/release'
		}
		maven {
			name 'Fabric'
			url 'https://maven.fabricmc.net'
		}
	}

	license {
		rule file("${rootDir}/codeformat/HEADER")

		include '**/*.java'
	}

	java {
		sourceCompatibility = JavaVersion.VERSION_17
		targetCompatibility = JavaVersion.VERSION_17

		withSourcesJar()
	}

	publishing {
		repositories {
			maven {
				url = System.getenv('MAVEN_URL')
				credentials {
					username = System.getenv('MAVEN_USERNAME')
					password = System.getenv('MAVEN_PASSWORD')
				}
			}
		}
	}
}
